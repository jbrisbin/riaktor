/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Riak FastData library built on Reactor'

ext {
	gradleVersion = '1.8'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.2.0-beta-2'

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	// Riak
	riakPbVersion = '1.4.0'

	// Reactor
	reactorVersion = '1.0.0.BUILD-SNAPSHOT'

	// Libraries
	jacksonVersion = '2.2.3'

	// Testing
	spockVersion = '0.7-groovy-2.0'
}

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'propdeps'
apply plugin: 'idea'
apply from: "$gradleScriptDir/setup.gradle"

configurations.all {
	exclude group: "commons-logging"
	exclude module: "junit"
}

group = 'com.jbrisbin.riaktor'
repositories {
	maven { url 'http://repo.spring.io/libs-snapshot' }
	mavenCentral()
}

project.sourceCompatibility = 1.7
project.targetCompatibility = 1.7

[compileJava, compileTestJava]*.options*.compilerArgs = [
		"-Xlint:serial",
		"-Xlint:varargs",
		"-Xlint:cast",
		"-Xlint:classfile",
		"-Xlint:dep-ann",
		"-Xlint:divzero",
		"-Xlint:empty",
		"-Xlint:finally",
		"-Xlint:overrides",
		"-Xlint:path",
		"-Xlint:processing",
		"-Xlint:static",
		"-Xlint:try",
		"-Xlint:-options", // intentionally disabled
		"-Xlint:-fallthrough", // intentionally disabled
		"-Xlint:-rawtypes", // TODO enable and fix warnings
		"-Xlint:-deprecation", // TODO enable and fix warnings
		"-Xlint:-unchecked" // TODO enable and fix warnings
]

compileJava {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

compileTestJava {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

test {
	systemProperty("java.awt.headless", "true")
	systemProperty("testGroups", project.properties.get("testGroups"))
	scanForTestClasses = false
	include '**/*Spec.*'
	include '**/*Tests.*'
	exclude '**/*Abstract*.*'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
	classifier = 'groovydoc'
	from groovydoc
}

artifacts {
	archives sourcesJar
	archives groovydocJar
}

// dependencies that are common across all java projects
dependencies {
	// Logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"

	// Reactor
	compile "org.projectreactor:reactor-core:$reactorVersion",
			"org.projectreactor:reactor-groovy:$reactorVersion",
			"org.projectreactor:reactor-tcp:$reactorVersion"

	// Riak
	compile "com.basho.riak.protobuf:riak-pb:$riakPbVersion"

	// Jackson
	compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

	// Libraries
	compile "commons-codec:commons-codec:1.8"

	// Groovy
	compile "org.codehaus.groovy:groovy-all:$groovyVersion"

	// Testing
	testCompile "org.spockframework:spock-core:$spockVersion",
			"org.hamcrest:hamcrest-library:1.3"
	testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion",
			"ch.qos.logback:logback-classic:$logbackVersion"
}


