/*
 * Copyright (c) 2011-2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Data components'

ext {
	gradleVersion = '1.6'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Languages
	groovyVersion = '2.1.4'

	// Logging
	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.11'

	// Riak
	riakClientVersion = '1.1.0'

	// Reactor
	reactorVersion = '1.0.0.BUILD-SNAPSHOT'

	// Testing
	spockVersion = '0.7-groovy-2.0'
}

apply from: "$gradleScriptDir/setup.gradle"
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'groovy'

configurations.all {
	exclude group: "commons-logging"
	exclude module: "junit"
}

group = 'riaktor'
repositories {
	mavenLocal()
	maven { url 'http://repo.springsource.org/libs-release' }
	maven { url 'http://repo.springsource.org/libs-snapshot' }
	mavenCentral()
}

project.sourceCompatibility = 7
project.targetCompatibility = 7

[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:none", "-g"]

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
	classifier = 'groovydoc'
	from groovydoc
}

artifacts {
	archives sourcesJar
	archives groovydocJar
}

// dependencies that are common across all java projects
dependencies {
	// Logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"

	// Reactor
	compile "reactor:reactor-core:$reactorVersion"

	// Riak
	compile "com.basho.riak:riak-client:$riakClientVersion"

	// Testing
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
	testCompile "org.hamcrest:hamcrest-library:1.3"
	testCompile "org.spockframework:spock-core:$spockVersion"
	testCompile "reactor:reactor-groovy:$reactorVersion"
	testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	testRuntime "reactor:reactor-logback:$reactorVersion"
}


